def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

group 'com.kanbanbox.nimmsta_sdk'
version '1.0'

buildscript {
    ext.kotlin_version = '1.4.32'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://maven.goma-cms.org/repository/nimmsta-core-release/'
            credentials {
                username = localProperties.getProperty('nimmsta.username')
                password = localProperties.getProperty('nimmsta.password')
            }
            authentication {
                basic(BasicAuthentication)
            }
            metadataSources {
                mavenPom()
                artifact()
            }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }
}

dependencies {
    api("com.nimmsta.core:android:5.0.+@aar") {
        transitive = true
        changing = true
    }
    implementation "androidx.appcompat:appcompat:1.2.0"
}